subformula_without_brackets = /[^=,\(\)\[\]<>\n\r']+/ ;
subformula = [ subformula_without_brackets ] "(" subformula ")" [ subformula ] | subformula_without_brackets ;
formula = subformula | "'" @:/[^']*/ "'" ;

function_name = /\w+/;
parameter_name = /\w+/;
parameter_value = formula | "[" [ @+:parameter_value { "," @+:parameter_value } ] "]";
parameter = parameter_name "=" parameter_value | parameter_value ;
parameters = [ @+:parameter { "," @+:parameter } ];
function = name:function_name "(" parameters:parameters ")";

variable_name = /[\w]+/;
longname = '"' @:/[^\"]+/ '"' | @:{ /[^,\[\]=\(\)\{\}<>\s]+/ /[\t ]+/ &/[^,\[\]=\(\)\{\}<>\s]+/ }+ ;
uncertainty = "<" @:formula ">";
unit = "[" @:formula "]";

assignment = [longname:longname] name:variable_name "=" value:formula [ uncertainty:uncertainty ] [ unit:unit ] ;

newline = ["\r\n" | "\n" | "\r"];
whitespace = { newline };

multi_assignment_spec = [longname:longname] name:variable_name [ uncertainty:uncertainty ] [ unit:unit ];
multi_assignment_header = @+:multi_assignment_spec { "," @+:multi_assignment_spec } newline;
multi_assignment_value = /[\w\.]+/;
multi_assignment_row = { @+:multi_assignment_value }+ newline;
multi_assignment_rows = { @+:multi_assignment_row whitespace };
multi_assignment = "{" whitespace header:multi_assignment_header whitespace rows:multi_assignment_rows whitespace "}";

python_line = ">" @:/[^\n\r]*/;
python_code = ( code+:python_line { newline code+:python_line });

command = @:( assignment | function | multi_assignment | python_code ) (newline | $);
program = whitespace { @+:command whitespace } $;
